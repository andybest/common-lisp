(in-package #:cl-user)

(defpackage #:origin.swizzle
  (:local-nicknames (#:a #:alexandria)
                    (#:u #:golden-utils))
  (:use #:cl))

(in-package #:origin.swizzle)

(u:eval-always
  (defun %swizzle/combinations (n items)
    (if (= n 1)
        (mapcar #'list items)
        (mapcan
         (lambda (x)
           (mapcar
            (lambda (y)
              (cons x y))
            (%swizzle/combinations (1- n) items)))
         items)))

  (defun %swizzle/component-groups ()
    (loop :for masks :in '((x y z w) (r g b a) (s t p q))
          :append
          (loop :with set = (subseq masks 0 4)
                :for i from 1 :to 4
                :for items = (%swizzle/combinations i set)
                :append (mapcar (lambda (x) (format nil "狺┅轸屙螬┅ㄤ彐躅ン鏖戾汨狎痫箝糸镱ㄣ镯痫铄铘轭溴戾è汨狎ㄣ栳泔眇镱孱趔轭溴┅矧痫箝糸镱汨狎⒇仝注痫箝糸镱汨狎⒁锹立痫箝糸镱汨狎⒂孕癣┅┅ㄤ彐躅ン鏖戾骢钽糸镱怙澌ㄣ镯痫铄铘螬戾è箝戾铉翳泔眇镱孱趔┅ㄩ箝暴啜狎彐鲥ē篦辁戾汨狎痫箝糸镱泔眇镱孱趔癌啜戾è蝈篚祠磲脲狎蜥箝哄戾礤铘豉疱箝铉戾骒镝舂┅括祜镳烘矧衡屐秣箝烘矧痫ē篦辁戾汨狎痫箝糸镱泔眇镱孱趔椹恒镬戾泗啜箦翩ㄡ蝈蝈篚祠椹ㄡ蝈鲥痫螬┅蝈篚祠┅┅ㄤ彐躅ン鏖戾珏铄蜥翦滹泱趄轭ㄣ镯痫铄铘螬戾è箝戾铉翳泔眇镱孱趔┅ㄩ箝暴ㄦ矧磲铋⒂鏖戾清翳筱犰狎泔眇镱孱骝镯峙卯泔眇镱孱趔ㄦ矧磲铋⒂鏖戾抿遽翦鲥泾骝镯翳狺［滑犷夯蔺泔眇镱孱趔镦峙卯箝ㄣ镥蜚泔眇镱孱趔ъ轶舂┅┅磲泸镬弭èョ孱弪狒瀛篦辁戾骢钽糸镱ī啜痱镧括祜镳烘矧泔眇镱孱趔洪ē篦辁戾泔眇镱孱舡珧秕痼烘矧骢钽钺礤ㄡ烘矧磲舡簌礅镬疳汶徵濯豪ó狺泔眇镱孱趔烘矧泔眇镱孱舡扉篝ㄣ镥蜚泔眇镱孱趔ъ轶舂横痧孱啜ㄤ邈灬轫ㄩ铎轭骢钽钺礤┅ㄥ痫螋К骢钽钺礤箦翩ㄤ镢蹴孱翎糸镱К骢钽钺礤ф躅泗轱瞟ē篦辁戾珏铄蜥翦滹泱趄轭泔眇镱孱趔┅ㄤ彐躅骢钽钺礤鲥悌ē篦辁戾骢钽糸镱怙澌泔眇镱孱趔┅┅┅ē珏铄蜥翦篦辁戾骢钽糸镱螬