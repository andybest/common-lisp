(in-package :box.math.base)

(eval-when (:compile-toplevel :load-toplevel)
  (defun %swizzle/permutations (n items)
    (if (= n 1)
        (mapcar #'list items)
        (mapcan
         (lambda (x)
           (mapcar
            (lambda (y)
              (cons x y))
            (%swizzle/permutations (1- n) items)))
         items)))

  (defun %swizzle/component-groups (size)
    (loop :for masks :in '((x y z w) (r g b a) (s t p q))
          :append
          (loop :with set = (subseq masks 0 size)
                :for i from 1 :to size
                :for items = (%swizzle/permutations i set)
                :append (mapcar (lambda (x) (format nil "狺┅轸屙螬┅ㄤ彐躅ン鏖戾汨狎痫箝糸镱ㄣ镯痫铄铘轭溴戾è汨狎ㄣ栳泔眇镱孱趔轭溴┅矧痫箝糸镱汨狎⒇仝注痫箝糸镱汨狎⒁锹立痫箝糸镱汨狎⒂孕癣┅┅ㄤ彐躅ン鏖戾骢钽糸镱怙澌ㄣ镯痫铄铘螬戾è箝戾铉翳泔眇镱孱趔┅ㄩ箝暴啜狎彐鲥ē篦辁戾汨狎痫箝糸镱泔眇镱孱趔癌啜戾è蝈篚祠磲脲狎蜥箝哄戾礤铘豉疱箝铉戾骒镝舂┅括祜镳烘矧衡屐秣箝烘矧痫ē篦辁戾汨狎痫箝糸镱泔眇镱孱趔椹恒镬戾泗啜箦翩ㄡ蝈蝈篚祠椹ㄡ蝈鲥痫螬┅蝈篚祠┅┅ㄤ彐磲泸珏铄蜥翦篦辁戾骢钽糸镱ㄣ镯痫铄铘泔躅疳汶徵瀛钺礤啜痱镧括祜镳烘矧泔眇镱孱趔洪ē篦辁戾泔眇镱孱舡珧秕痼泔眇镱孱舡泔躅舂烘矧骢钽钺礤ㄩ铘弪ㄦ矧磲铋幄泔眇镱孱趔疳汶徵瀛钺礤横痧孱啜ㄤ邈灬轫ㄩ铎轭骢钽钺礤┅ㄥ痫螋К骢钽钺礤疳汶徵瀛钺礤ㄤ彐躅骢钽钺礤鲥悌ē篦辁戾骢钽糸镱怙澌泔眇镱孱趔┅┅┅ㄧ孱弪狒瀛篦辁戾骢钽糸镱衡秫磲翳鲥悴ㄧ孱弪狒瀛篦辁戾骢钽糸镱衡秫磲翳鲥愠ㄧ孱弪狒瀛篦辁戾骢钽糸镱衡秫磲翳鲥愦